# ************************************************************************* #
# This file is part of Shard.                                               #
#                                                                           #
# Shard is free software: you can redistribute it and/or modify             #
# it under the terms of the GNU Affero General Public License as            #
# published by the Free Software Foundation.                                #
#                                                                           #
# This program is distributed in the hope that it will be useful,           #
# but WITHOUT ANY WARRANTY; without even the implied warranty of            #
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the              #
# GNU Affero General Public License for more details.                       #
#                                                                           #
# You should have received a copy of the GNU Affero General Public License  #
# along with this program. If not, see <http://www.gnu.org/licenses/>.      #
# ************************************************************************* #

EOL:
    0x0A
    0x0D 0x0A

WHITE_SPACE:
    SPACE
    SPACE WHITE_SPACE

SPACE:
    0x20
    0x09

# TOKEN: Comment
COMMENT:
    BLOCK_COMMENT
    LINE_COMMENT

BLOCK_COMMENT:
    '/*' CHARACTERS '*/'

LINE_COMMENT:
    '//' CHARACTERS EOL

CHARACTERS:
    CHARACTER
    CHARACTER CHARACTERS

CHARACTER:
    UTF8_CHAR

LETTER:
    'a'
    'b'
    'c'
    'd'
    'e'
    'f'
    'g'
    'h'
    'i'
    'j'
    'k'
    'l'
    'm'
    'n'
    'o'
    'p'
    'q'
    'r'
    's'
    't'
    'u'
    'v'
    'w'
    'x'
    'y'
    'z'
    'A'
    'B'
    'C'
    'D'
    'E'
    'F'
    'G'
    'H'
    'I'
    'J'
    'K'
    'L'
    'M'
    'N'
    'O'
    'P'
    'Q'
    'R'
    'S'
    'T'
    'U'
    'V'
    'W'
    'X'
    'Y'
    'Z'

DIGIT:
    '0'
    '1'
    '2'
    '3'
    '4'
    '5'
    '6'
    '7'
    '8'
    '9'

# TOKEN: Identifier
IDENTIFIER:
    IDENTIFIER_START
    IDENTIFIER_START IDENTIFIER_CHARS

IDENTIFIER_CHARS:
    IDENTIFIER_CHAR
    IDENTIFIER_CHAR IDENTIFIER_CHARS

IDENTIFIER_START:
    '_'
    LETTER

IDENTIFIER_CHAR:
    '_'
    LETTER
    DIGIT

# TOKEN: StringLiteral
STRING_LITERAL:
    '"' STRING_LITERAL_CHARACTERS '"'

STRING_LITERAL_CHARACTERS:
    STRING_LITERAL_CHARACTER
    STRING_LITERAL_CHARACTER STRING_LITERAL_CHARACTERS

STRING_LITERAL_CHARACTER:
    CHARACTER
    ESCAPE_SEQUENCE
    EOL

ESCAPE_SEQUENCE:
    '\''
    '\"'
    '\?'
    '\\'
    '\0'
    '\a'
    '\b'
    '\f'
    '\n'
    '\r'
    '\t'
    '\v'

# TOKEN: CharLiteral
CHAR_LITERAL:
    ''' CHAR_LITERAL_CHARACTER '''

CHAR_LITERAL_CHARACTER:
    CHARACTER
    ESCAPE_SEQUENCE

# TOKEN: NumberLiteral
NUMBER_LITERAL:
    DECIMAL_DIGIT IDENTIFIER_CHARS

# TOKEN: Other
OTHER:
    ASCII_CHAR

TOKEN:
    IDENTIFIER
    STRING_LITERAL
    CHAR_LITERAL
    NUMBER_LITERAL
    OTHER
    COMMENT

# START
TOKENS:
    TOKEN
    TOKEN WHITE_SPACE TOKENS

# ************************************************************************* #
